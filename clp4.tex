\clearpage

\section{A SICStus \clpb könyvtára}

A következõ fejezetben a SICStus \clpb könyvtárával fogunk foglalkozni.
A \clpb könyvtárat az alábbi módon lehet használatba venni:
\begin{verbatim}
:- use_module(library(clpb)).
\end{verbatim}

\subsection{A \clpb könyvtár általános jellemzése}

A \clpb könyvtár a kétértékû Boole-logikán alapuló CLP rendszert valósít meg,
ennek megfelelõen a \clpb változók értékkészlete a ${0,1}$ halmaz.

\enumhead{Felhasználható függvények (egyben korlát-relációk)}
\btab{lp{30em}}
\cd{{\~{}} P}      &  \cd{P} hamis ({\it negáció}).\\
\cd{P * Q}     &  \cd{P} és \cd{Q} mindegyike igaz ({\it konjunkció}).\\
\cd{P + Q}     &  \cd{P} és \cd{Q} legalább egyike igaz ({\it
diszjunkció}).\\
\cd{P \# Q}    &  \cd{P} és \cd{Q} pontosan egyike igaz ({\it kizáró
vagy}).\\
\cd{X {\^{}} P}  &  Létezik olyan \cd{X}, hogy \cd{P} igaz (azaz \cd{P[X/0]+P[X/1]} igaz).\\
\verb'P =\= Q'  &  Ugyanaz, mint \verb'P # Q'.\\
\verb'P =:= Q'  &  Ugyanaz, mint \verb'~(P # Q)'.\\
\verb'P =< Q'   &  Ugyanaz, mint \verb'~P + Q'.\\
\verb'P >= Q'   &  Ugyanaz, mint \verb'P + ~Q'.\\
\verb'P < Q'    &  Ugyanaz, mint \verb'~P * Q'.\\
\verb'P > Q'    &  Ugyanaz, mint \verb'P * ~Q'.\\
\cd{card(Is, Es)} &  Az \cd{Es} listában szereplõ igaz értékû
kifejezések száma eleme az \cd{Is} által jelölt halmaznak (\cd{Is}
egészek és \cd{Tol-Ig} szakaszok listája).\\
\etab

A \Clpb -ben az összes korlát egyszerû korlát, nincsenek összetett korlátok.
Pont ez a tény az, ami a \Clpb -t nagy feladatok megoldására alkalmatlanná
teszi, hiszen minden korlát azonnal bekerül a korlát-tárba, és egy idõ után
a megoldó algoritmus (a Boole-egyesítés) mûködése a korlát-tár nagy mérete
miatt lelassul.

\enum{Alapvetõ könyvtári eljárások}{
\item \cd{sat(Kifejezés)} -- hozzáveszi \cd{Kifejezés}t a korlát-tárhoz.
\cd{Kifejezés} a \cd{0} és \cd{1} konstansokból, atomokból, valamint
változókból a fenti mûveletekkel felépített logikai kifejezés. A kifejezésben
elõforduló atomok a kifejezés legkülsõ szintjén univerzálisan kvantifikált
változókat jelentenek.

\item \cd{taut(Kifejezés,Érték)} -- megvizsgálja, hogy \cd{Kifejezés}
levezethetõ-e a korlát-tárból. Ha levezethetõ, akkor \cd{Érték}et \cd{1}-gyel
egyesíti. Ha \cd{Kifejezés} tagadása levezethetõ, akkor \cd{Érték}et \cd{0}-val
egyesíti. Minden más esetben meghiúsul.

\item \cd{labeling(Változók)} -- beállítja a \cd{Változók} lista összes
elemét 1-re vagy 0-ra úgy, hogy a korlát-tár teljesüljön. Visszalépésre az
összes lehetséges megoldást felsorolja.
}

\subsection{Példafutások a \clpb könyvtár segítségével}

\begin{verbatim}
| ?- sat(X + Y).
sat(X=\=_A*Y#Y) ? 

| ?- sat(x + Y).
sat(Y=\=_A*x#x) ? 

| ?- taut(_A ^ (X=\=_A*Y#Y) =:= X+Y, T).
T = 1 ? 

| ?- sat(A # B =:= 0).
B = A ? 

| ?- sat(A # B =:= C), A = B.
B = A, C = 0 ? 

| ?- taut(A =< C, T).
no

| ?- sat(A =< B), sat(B =< C), taut(A =< C, T).
T = 1, sat(A=:=_A*_B*C), sat(B=:=_B*C) ? 
\end{verbatim}

Látható, hogy a \clpb a korlát-tár tartalmát \cd{sat(Kifejezés)} alakú
struktúrák konjunkciójaként jeleníti meg, ahol \cd{Kifejezés} mindig egy
,,polinom'', azaz konjunkciók kizáró vagy (\cd{\#}) mûveletekkel képzett
sorozata. Az atomok a fent elmondottak szerint univerzálisan kvantifikált
változókat jelentenek. Az univerzális és az egzisztenciális kvantifikáció
különbségének kiemelésére nézzük meg az alábbi példákat is:

\begin{alltt}
| ?- sat(~x+ ~y=:= ~(x*y)).   % \(\forall\cd{xy}(\lnot\cd{x}\lor\lnot\cd{y}=\lnot(\cd{x}\land\cd{y}))\)
yes
| ?- sat(~X+ ~Y=:= ~(X*Y)).   % \(\exists?\cd{XY}(\lnot\cd{X}\lor\lnot\cd{Y}=\lnot(\cd{X}\land\cd{Y}))\)
true ? ; no
| ?- sat(x=<y).               % \(\forall\cd{xy}(\cd{x} \to \cd{y})\)
no
| ?- sat(X=<y).               % \(\forall\cd{y}\exists?\cd{X}(\cd{X} \to \cd{y})\) 
sat(X=:=_A*y) ? ; no
\end{alltt}

Nézzünk most egy picit komplikáltabb \clpb példát, amely egy egy bites
összeadó áramkör mûködését próbálja modellezni \clpb korlátok segítségével:

\begin{verbatim}
| ?- [user].
| adder(X, Y, Sum, Cin, Cout) :-
     sat(Sum =:= card([1,3],[X,Y,Cin])),
     sat(Cout =:= card([2-3],[X,Y,Cin])).
| {user consulted, 40 msec 576 bytes}
yes
\end{verbatim}

Az összeadó mûködése a \cd{card/2} segítségével nagyon egyszerûen leírható:
az összeg értéke akkor 1, ha az \cd{[X,Y,Cin]} listában (\cd{Cin} a carry in,
tehát a bemenõ átvitel rövidítése) 1 vagy 3 db egyes van, \cd{Cout}
(a kimenõ átvitel) értéke pedig akkor 1, ha az \cd{[X,Y,Cin]} listában legalább
két db egyes van. Nézzük meg, hogy ezeket a korlátokat milyen formában tárolja
a \clpb rendszer!

\begin{verbatim}
| ?- adder(x, y, Sum, cin, Cout).
sat(Sum=:=cin#x#y),
sat(Cout=:=x*cin#x*y#y*cin) ?
\end{verbatim}

Látható, hogy a \cd{card/2} itt is konjunkciók kizáró vagy kapcsolatára
vezetõdött vissza. Mivel csak a \cd{Sum} és \cd{Cout} változók viselkedésére
voltunk kíváncsiak, ezért a másik három változót egzisztenciálisan
kvantifikáltuk. Nézzük meg azt az egyszerûsített esetet is, amikor \cd{Cin}-t
0-ra kötjük, tehát nincs bemenõ átvitel:

\begin{verbatim}
| ?- adder(x, y, Sum, 0, Cout).
sat(Sum=:=x#y),
sat(Cout=:=x*y) ?
\end{verbatim}

Ha a megoldás nem egyértelmû, akkor a \cd{labeling} eljárással tudjuk
felsoroltatni az összes lehetséges megoldást:

\begin{verbatim}
| ?- adder(X, Y, 0, Cin, 1), labeling([X,Y,Cin]).
Cin = 0, X = 1, Y = 1 ? ; 
Cin = 1, X = 0, Y = 1 ? ;
Cin = 1, X = 1, Y = 0 ? ;
no
\end{verbatim}

\subsection{A Boole-egyesítés}

A \clpb könyvtár két Boole-kifejezés egyesítésére ,,meglepõ módon'' a
Boole-egyesítés nevû algoritmust használja. A feladat pontos megfogalmazása:
legyen adott a $g$ és $h$ Boole-kifejezés, és keressük a $g = h$
egyenletet megoldó legáltalánosabb egyesítõt (a továbbiakban ezt $mgu(g,h)$-val
jelöljük). Mivel a $g = h$ egyenlet helyettesíthetõ a $g \oplus h = 0$ egyenlettel
(ahol $\oplus$ a kizáró vagy mûveletet jelenti, amit \Clpb -ben a \cd{\#}
operátor jelöl), ezért a továbbiakban a Boole-egyesítés vizsgálatához
elegendõ az $f=0$ alakú egyenletek megoldását vizsgálnunk. Az egyesítés
minden lépése során egy $f=0$-beli $x$ formulaváltozót szeretnénk kifejezni a
többi segítségével.
\br
Legyen $f_x(1)$ az $f$-bõl az $x=1$ helyettesítéssel, az $f_x(0)$ pedig az
$f$-bõl az $x=0$ helyettesítéssel kapott formula. $f=0$ kielégíthetõségének
szükséges feltétele $f_x(1) \land f_x(0) = 0$ kielégíthetõsége. Fejezzük ki
$x$-et $f_x(1)$ és $f_x(0)$ segítségével úgy, hogy $f=0$ legyen!

\btab{|l|l|l|}
\hline $f_x(0)$ & $f_x(1)$ & $x$ \\
\hline 0        & 0        & bármi ($w$)\\
\hline 0        & 1        & 0\\
\hline 1        & 0        & 1\\
\hline 1        & 1        & érdektelen\\
\hline
\etab

Ha $x$-et $x=(a \land \overline{w}) \oplus (b \land w)$ (Prolog: \cd{X=A*{\~{}}W \# B*W})
alakban keressük, akkor a fentiek szerint $a$ és $b$ értéke az alábbiak
szerint adódik:

\begin{center}
\begin{tabular}{|l|l|l||l|l|}
\hline $f_x(0)$ & $f_x(1)$ & $x$ & $a$ & $b$ \\
\hline 0        & 0        & $w$ & 0   & 1   \\
\hline 0        & 1        & 0   & 0   & 0   \\
\hline 1        & 0        & 1   & 1   & 1   \\
\hline
\end{tabular}
\end{center}

A táblázat alapján az $a=f_x(0)$ és $b=\overline{f_x(1)}$ megfeleltetés tûnik
a legegyszerûbbnek. Így alapján az egyesítési algoritmus mûködése az $f=0$
alakú egyenlõségekre:

\begin{enumerate}
\item Ha $f$-ben nincs változó, akkor $f$-nek azonosan 0-nak kell lennie,
      egyébként ugrás a következõ pontra.
\item Helyettesítsünk $f$-ben egy tetszõleges $x$ változót az
      $x=(f_x(0) \land \overline{w}) \oplus (\overline{f_x(1)} \land w)$ kifejezéssel
      (Prolog: \cd{X=}$f_x(0)$\cd{*{\~{}}W \# {\~{}}}$f_x(1)$\cd{*W}).
\item Folytassuk az egyesítést az $f_x(1) \land f_x(0) = 0$ egyenlõségre
\end{enumerate}

Példák a Boole-egyesítésre:

\begin{itemize}
\item $mgu$(\cd{X+Y}, \cd{0}) $\longrightarrow$ \cd{X = 0, Y = 0};
\item $mgu$(\cd{X+Y}, \cd{1}) = $mgu$(\verb'~(X+Y)', \cd{0})
$\longrightarrow$ \cd{X = W * Y \# Y \# 1};
\item $mgu$(\cd{X*Y}, \verb'~(X*Z)') = $mgu$(\cd{(X*Y)\#(X*Z)\#1}, \cd{0})
$\longrightarrow$ \cd{X = 1}, \cd{Y = {\~{}}Z}.
\end{itemize}

\subsection{A \clpb belsõ ábrázolási formája}

A \clpb könyvtár a Boole-kifejezéseket az úgynevezett \emph{Boole/bináris
döntési diagram}ok (\emph{Boole/Binary Decision Diagram}s, \emph{BDD})
segítségével ábrázolja. Ezek irányított körmentes gráfok (\emph{directed
acyclic graph}, \emph{DAG}), amelyekben kétféle csomópont és kétféle él
szerepel. A csomópontok tartozhatnak változóhoz vagy a 0 és 1 konstansokhoz.
Csak a változókat tartalmazó csomópontokból indulhat ki él, mégpedig
mindkettõbõl kétfajta, az egyik a hamis értéknek (az ábrákon szaggatott vonal),
a másik az igaz értéknek (az ábrákon folytonos vonal) felel meg. Az egyik
csomópont kitüntetett abból a szempontból, hogy ebbe a csomópontba egy
végpont nélküli él is befut (ezt a csomópontot hívjuk \emph{kezdõ csomópont}nak).
A BDD-k segítségével meghatározható az általuk reprezentált Boole-kifejezés
értéke. Ehhez a gráfot a kezdõ csomópontból kezdve kell bejárni a
következõ szabályok szerint:

\begin{enumerate}
\item Ha az aktuális csomópont a 0 vagy az 1 konstansot tartalmazza, akkor
megállhatunk a bejárással, és a kifejezés értéke 0 vagy 1, a csomópont
tartalmától függõen.
\item Ha az aktuális csomópont változót tartalmaz, akkor a változó aktuális
értékétõl függõen vagy a szaggatott (0-nak megfelelõ), vagy a folytonos
(1-nek megfelelõ) él mentén kell folytatni a bejárást.
\end{enumerate}

Mivel a gráf irányított, körmentes, és csak a 0-t vagy 1-et tartalmazó
csomópontok nyelõk, ezért az algoritmus véges idõn belül le fog futni.
A könnyebb érthetõség kedvéért álljon itt két Boole-kifejezés bináris döntési
diagramja:

\btab{cp{1.5cm}c}
\verb'(X+Y) # 1' & & \verb'X*Y # X*Z # 1'  \\
\hspace*{1cm}\raisebox{2cm}{\epsfig{file=bdd2.eps, width=0.23\linewidth}} & & \epsfig{file=bdd1.eps, width=0.3\linewidth}  \\
\etab

\subsection{Összetett \clpb példa: hibakeresés áramkörben}

Legyen adott egy egybites összeadó áramköri modellje (funkcionális elemekkel
és összeköttetésekkel). Írjunk egy olyan \clpb programot, amely adott
bemenet-kimenet párra megmondja, hogy melyik funkcionális elem mûködik
hibásan, ha feltételezzük, hogy egyszerre csak egy hibásodik meg!

\begin{center}\insertfig{adder1}{10cm}\end{center}

\begin{verbatim}
fault([F1,F2,F3,F4,F5], [X,Y,Cin], [Sum,Cout]) :-
        sat(
                    card([0-1],[F1,F2,F3,F4,F5]) *
                    (F1 + (U1 =:= X * Cin)) *
                    (F2 + (U2 =:= Y * U3)) *
                    (F3 + (Cout =:= U1 + U2)) *
                    (F4 + (U3 =:= X # Cin)) *
                    (F5 + (Sum =:= Y # U3))
                ).
\end{verbatim}

Amint látható, a \cd{fault/3} predikátumban semmi mást nem tettünk, mint
specifikáltuk az egyes áramköri elemek mûködését, valamint a \cd{card}
segítségével megadtuk azt a tényt is, hogy egyszerre legfeljebb egy
áramköri elem hibás. Lássunk néhány példát a \cd{fault} mûködésére!

\begin{verbatim}
| ?- fault(L, [1,1,0], [1,0]).
                            L = [0,0,0,1,0] ? ;  no
\end{verbatim}

Itt a \cd{fault} helyesen kikövetkeztette, hogy ilyen bemenet-kimenet
pár esetén csak a 4. funkcionális elem, azaz a programkód alapján
az \cd{X}-re és \cd{Cin}-re kapcsolódó XOR kapu lehet hibás.

\begin{verbatim}
| ?- fault(L, [1,0,1], [0,0]).
                            L = [_A,0,_B,0,0],
                            sat(_A=\=_B) ? ; no
\end{verbatim}

Itt a \cd{fault} úgy gondolja, hogy vagy az elsõ, vagy a harmadik funkcionális
elem a hibás, ezt a korlát-tárban lévõ \cd{sat(_A=\bs=_B)} fejezi ki. Ha
ehelyett látni szeretnénk a két lehetõséget teljesen behelyettesített
változókkal, akkor szükséges a \cd{labeling} használata is:

\begin{verbatim}
| ?- fault(L, [1,0,1], [0,0]), labeling(L).
                            L = [1,0,0,0,0] ? ;
                            L = [0,0,1,0,0] ? ; no
\end{verbatim}

Végül megkérdezhetjük a \cd{fault}-tól, hogy helyes áramköri mûködést
feltételezve mik az egyes kimenetekhez rendelhetõ logikai egyenletek:

\begin{verbatim}
| ?- fault([0,0,0,0,0], [x,y,cin], [Sum,Cout]).
                            sat(Cout=:=x*cin#x*y#y*cin),
                            sat(Sum=:=cin#x#y) ? ; no
\end{verbatim}

Tekintsünk most egy XOR kaput megvalósító tranzisztoros áramkört, és
valósítsuk meg ennek a verifikációját \clpb programmal!

\begin{center}\insertfig{xor}{8cm}\end{center}

Elõször leírjuk az npn és pnp tranzisztorok mûködését \clpb kifejezésekkel:
\begin{verbatim}
n(D, G, S) :-    % Gate => Drain = Source
        sat( G*D =:= G*S).

p(D, G, S) :-    % ~ Gate => Drain = Source
        sat( ~G*D =:= ~G*S).
\end{verbatim}

Ezek segítségével megfogalmazhatjuk az XOR kapu mûködését, ha a földelést
0-val, a tápfeszültséget 1-gyel jelöljük:

\begin{verbatim}
xor(A, B, Out) :-
        p(1, A, X),
        n(0, A, X),
        p(B, A, Out),
        n(B, X, Out),
        p(A, B, Out),
        n(X, B, Out).
\end{verbatim}

Ellenõrizzük, hogy az npn és pnp tranzisztorokra felírt egyenleteink
helyesen mûködnek-e a gate feszültség ki- és bekapcsolására:

\begin{verbatim}
| ?- n(D, 1, S).                 S = D ?
| ?- n(D, 0, S).                 true ?
| ?- p(D, 0, S).                 S = D ?
| ?- p(D, 1, S).                 true ?
\end{verbatim}

A fentiek szerint a tranzisztorok logikai felírása helyes, így nem
maradt más hátra, mint hogy ellenõrizzük, hogy az XOR kapu ténylegesen
az XOR függvényt valósítja-e meg:

\begin{verbatim}
| ?- xor(a, b, X).               sat(X=:=a#b) ?
\end{verbatim}

\subsection{Aknakeresõ játék \Clpb -ben}

Az alábbi programkód egy aknakeresõ játékot valósít meg \clpb programkód
formájában.

\begin{verbatim}
:- use_module([library(clpb),library(lists)]).
\end{verbatim}
\begin{verbatim}
mine(Rows, Cols, Mines, Bd) :-
        length(Bd, Rows), all_length(Bd, Cols), 
        append_lists(Bd, All),
        sat(card([Mines], All)), play_mine(Bd, []).
\end{verbatim}
\begin{verbatim}
all_length([], _).
all_length([L|Ls], Len) :- 
        length(L, Len), all_length(Ls, Len).
\end{verbatim}
\begin{verbatim}
append_lists([], []).
append_lists([L|Ls], Es) :-
        append_lists(Ls, Es0), append(L, Es0, Es).
\end{verbatim}
\begin{verbatim}
play_mine(Bd, Asked) :- 
        select_field(Bd, Asked, R, C, E), !,
        format('Row ~w, col ~w (m for mine)? ', [R,C]), 
        read(Ans), process_ans(Ans, E, R, C, Bd), 
        play_mine(Bd, [R-C|Asked]).
play_mine(_Bd, _Asked).
\end{verbatim}
\begin{verbatim}
select_field(Bd, Asked, R, C, E) :-
        nth(R, Bd, L), nth(C, L, E), 
        non_member(R-C, Asked), taut(E, 0), !.
select_field(Bd, Asked, R, C, E) :-
        nth(R, Bd, L), nth(C, L, E), 
        non_member(R-C, Asked), \+ taut(E,1), !.
\end{verbatim}
\begin{verbatim}
process_ans(m, 1, _, _, _) :- 
        format('Mine!~n', []), !, fail.
process_ans(Ans, 0, R, C, Bd) :-
        integer(Ans), neighbs(n(R, C, Bd), Ns), 
        sat(card([Ans], Ns)).
\end{verbatim}
\begin{verbatim}
neighbs(RCB, N7) :-
        neighbour(-1,-1, RCB, [], N0), 
        neighbour(-1, 0, RCB, N0, N1),
        neighbour(-1, 1, RCB, N1, N2), 
        neighbour( 0,-1, RCB, N2, N3),
        neighbour( 0, 1, RCB, N3, N4), 
        neighbour( 1,-1, RCB, N4, N5),
        neighbour( 1, 0, RCB, N5, N6), 
        neighbour( 1, 1, RCB, N6, N7).
\end{verbatim}
\begin{verbatim}
neighbour(ROf, COf, n(R0, C0, Bd), Nbs, [E|Nbs]) :-
        R is R0+ROf, C is C0+COf, 
        nth(R, Bd, Row), nth(C, Row, E), !.
neighbour(_, _, _, Nbs, Nbs).
\end{verbatim}

A játék belépési pontja a \cd{mine(Rows,Cols,Mines,Bd)} eljárás, amely
egy \cd{Rows} $\times$ \cd{Cols} méretû táblát készít el a \cd{Bd}
listában, és felteszi róla, hogy \cd{Mines} db 1-es van benne.
Ezek után a \cd{play_mine/2} eljáráson keresztül kijelzi, hogy melyik
mezõ tartalmára kíváncsi, erre a felhasználónak az \cd{m.} karaktersorozattal
kell válaszolnia, ha akna van ott, üres mezõ esetén pedig meg kell adni, hogy
hány akna található a mezõ körül. Ha a felhasználó válasza \cd{m.}, akkor a
program szomorúan konstatálja, hogy veszített (\cd{Mine!}), ha viszont egy
szám, akkor a tippelt mezõ körül lévõ mezõkre a \cd{process_ans/5} eljárás
második klózával felveszi a megfelelõ számossági korlátot. A következõ tipp
kiválasztását a \cd{select_field/5} eljárás végzi, ez mindig egy olyan mezõt
választ ki, amelyrõl a program egyértelmûen tudja, hogy üres (1. klóz), ha ilyet
nem talál, akkor pedig egy olyat, amelyrõl nem tudja biztosan, hogy aknát tartalmaz
(2. klóz). Egy egyszerû példajáték ($3 \times 3$-as tábla, akna a 2. sor 1. mezõjén
és a 3. sor 3. mezõjén van):

\begin{verbatim}
| ?- mine(3,3,2,Bd).
% az elsõ két lépésben a program csak reménykedik abban, hogy
% nem lép aknára
Row 1, col 1 (m for mine)? 1.
Row 1, col 2 (m for mine)? 1.
% itt a program már rájön, hogy az (1,3) és a (2,3) mezõ üres
Row 1, col 3 (m for mine)? 0.
% ebbõl már azt is tudja, hogy a (2,2) mezõ üres, így viszont
% a (2,1) mezõn akna van
Row 2, col 2 (m for mine)? 2.
% mivel a (2,3) mezõrõl már elõzõleg kikövetkeztette, hogy üres,
% ezért azt is meg meri kérdezni
Row 2, col 3 (m for mine)? 1.
% mivel a (2,2) mezõ körül már csak 1 akna helyét nem tudjuk,
% de azt is tudjuk, hogy a (2,3) mezõ körül is 1 akna van, ezért
% a (3,1) mezõn nem lehet akna
Row 3, col 1 (m for mine)? 1.
% így viszont a (3,2) mezõ is üres
Row 3, col 2 (m for mine)? 2.
Bd = [[0,0,0],[1,0,0],[0,0,1]] ? ;
no
\end{verbatim}

Vegyük észre, hogy a program inkonzisztens adatok esetén meghiúsulást produkál:

\begin{verbatim}
| ?- mine(3,3,2,Bd).
Row 1, col 1 (m for mine)? 1.
Row 1, col 2 (m for mine)? 1.
Row 1, col 3 (m for mine)? 0.
Row 2, col 2 (m for mine)? 1.
no
\end{verbatim}

A \clpb mohósága miatt azonban a program egy $20 \times 20$-as aknamezõre már
nagyon hosszú ideig veszi fel a \cd{mine/4}-ben szereplõ \cd{sat(card...)}
korlátot (mivel a \cd{card}-ot is ,,polinom'' formára vezeti vissza a listában
szereplõ összes változó esetén), ilyenkor a program szinte játszhatatlanul lassú lesz.